import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp' version "$kotlin_version-$ksp_version"
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
}

android {
    namespace 'com.hegocre.nextcloudpasswords'
    compileSdk 34

    defaultConfig {
        applicationId "com.hegocre.nextcloudpasswords"
        minSdk 24
        targetSdk 34
        versionCode 24
        versionName "1.0.6"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        applicationVariants.configureEach { variant ->
            variant.resValue "string", "version_name", variant.versionName
            variant.resValue "string", "version_code", variant.versionCode.toString()
        }
    }

    androidResources {
        generateLocaleConfig = true
    }

    ksp {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
    }
    splits {
        abi {
            enable true
            universalApk true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.6'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
        jniLibs {
            keepDebugSymbols += ['**/*.so']
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.security:security-crypto:1.0.0'
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    implementation 'com.goterl:lazysodium-android:5.1.0@aar'
    implementation 'net.java.dev.jna:jna:5.13.0@aar'

    implementation 'org.commonmark:commonmark:0.21.0'
    implementation 'io.coil-kt:coil-compose:2.5.0'

    //Compose dependencies
    implementation platform('androidx.compose:compose-bom:2023.10.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.material3:material3:1.2.0-alpha12'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.navigation:navigation-compose:2.7.5'
    implementation 'androidx.activity:activity-compose:1.8.1'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation "androidx.autofill:autofill:1.1.0"

    //Room dependencies
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.2'

    implementation 'com.materialkolor:material-kolor:1.2.9'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.arch.core:core-testing:2.2.0'

    androidTestImplementation platform('androidx.compose:compose-bom:2023.10.01')
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
}

// Compiler flag to use experimental Compose APIs
tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        freeCompilerArgs.addAll("-opt-in=kotlin.RequiresOptIn", "-Xjvm-default=all-compatibility")
    }
}